pluginManagement {
    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }()

    def flutterVersionFile = file("$flutterSdkPath/version")
    if (!flutterVersionFile.exists()) {
        def flutterVersionJson = file("$flutterSdkPath/bin/cache/flutter.version.json")
        if (flutterVersionJson.exists()) {
            try {
                def json = new groovy.json.JsonSlurper().parseText(flutterVersionJson.text)
                def resolvedVersion = json.frameworkVersion ?: json.flutterVersion
                if (resolvedVersion != null) {
                    flutterVersionFile.text = resolvedVersion.toString().trim()
                }
            } catch (Exception e) {
                println("Warning: unable to generate Flutter version file at ${flutterVersionFile.path}: ${e.message}")
            }
        }
    }

    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/jcenter' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.9.1" apply false
    id "org.jetbrains.kotlin.android" version "2.1.0" apply false
}

include ":app"
